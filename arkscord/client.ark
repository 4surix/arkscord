# ArkScript 3.0.13
# ----------------------------------------------------------------------------

(import "http.arkm")
(import "json.arkm")

(import "./urls.ark")
(import "./objects/main.ark")
(import "./convert/main.ark")


(let version "0.0.1")


# Bot
(let create-bot
    (fun (token) {

        # Attribut
        (mut running true)
        (mut request (mk-request token))
        (mut channels-listened [])

        # Events
        (mut func_on-message nil)
        (let on-message (fun (func) (set func_on-message func)))

        # Methodes
        (let listen-channels-ids
            (fun (ids-channels) {
                (mut index 0)
                (while (< index (len ids-channels)) {
                    (let url (url-api-channel (@ ids-channels index)))
                    (let response (request.get url))
                    (if (not (nil? response))
                        (if (= 200 (@ response RESPONSE-STATUS))
                            {
                                (set channels-listened
                                    (append channels-listened
                                        (convert-json-object-channel 
                                            (json:fromString 
                                                (@ response RESPONSE-BODY)
                                ))))
                            }
                        )
                    )
                    (set index (+ 1 index))
                })
            })
        )
        (let run (quote {
            (while (= running true) {
                (if (not (nil? func_on-message))
                    {
                        (mut index 0)
                        (while (< index (len channels-listened)) {
                            (let messages (
                                (@ channels-listened index).get-new-messages
                                request
                            ))
                            (if (not (nil? messages))
                                {
                                    (mut index__ 0)
                                    (while (< index__ (len messages)) {
                                        (func_on-message (@ messages index__))
                                        (set index__ (+ index__ 1))
                                    })
                                }
                            )
                            (set index (+ index 1))
                            (sys:sleep (* 1 1000))
                        })
                    }
                )
            })
            (sys:sleep (* 1 1000))
        }))

        (fun (
                # Attributs
                &token
                &running
                &request
                &channels-listened

                # Events
                &on-message
                &func_on-message

                # Methodes
                &listen-channels-ids
                &run
            )
            ()
        )
    })
)
